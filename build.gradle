buildscript {
    ext {
        springBootVersion = '1.3.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE")
        classpath("org.flywaydb:flyway-gradle-plugin:3.2.1")

    }
}

plugins {
    id 'com.craigburke.bower-installer' version '2.4.1'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.flywaydb.flyway'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.security:spring-security-core:4.0.3.RELEASE")
    compile("org.flywaydb:flyway-core:3.2.1")
    compile("org.modelmapper:modelmapper:0.7.5")
    runtime("org.postgresql:postgresql:9.4-1206-jdbc42")
}

bower {
    installBase = 'src/main/resources/static/lib'

    dependencies {
        'jquery'('2.1.x') {
            source 'dist/*'
        }

        'jqueryui'()

        'underscore'('1.8.x') {
            source 'underscore*'
        }

        'bootstrap'('3.3.x') {
            source 'dist/js/bootstrap*' >> 'js/'
            source 'dist/css/*' >> 'css/'
            source 'dist/fonts/**' >> 'fonts/'
        }

        'basscss'('7.0.x')

        'html.sortable'('0.3.x')

        'backbone'('1.2.x') {
            source 'backbone*'
        }

        'marionette'('2.4.x') {
            source 'lib/backbone.marionette*'
        }

        'backbone.radio'('1.0.2') {
            source 'build/*'
        }

        'backbone.stickit'('0.9.2') {
            source 'backbone.stickit.js'
        }

        'requirejs'() {
            source 'require.js'
        }

        'requirejs-text'()
    }

    additional = [
            name: 'ConfLab'
    ]
}

assemble.dependsOn(bowerRefresh)
